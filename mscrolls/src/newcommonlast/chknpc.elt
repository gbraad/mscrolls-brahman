**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"

*--------------------------------
*
* 	CHKNPC Pops a spurious longword from the stack if the
*       NPC is NOT ok. This is meant to return an extra level
*       of routine, assuming that a return address is on the stack
*
*       NB Also corrupts A2 if the NPC is dead/asleep/etc.

	XDEF	CHKNPC

CHKNPC
	PUSH_L  D0/D1/A0/A3
	MOVE.W  D5,D0
	MOVE.L  A5,A0
	TST_DEAD				;
	BEQ.S   05$
	DO	P.BTN
	DO	ISARE
	MSG	DEAD
	BRA.S   90$
05$
	DO	GETNPC
	MOVE.B  2(A3),D1
	LSR.B	#4,D1		;just the emotion
	CMP.B   #5,D1
	BNE.S   10$
	DO	P.BTN
	DO	ISARE
	MSG	UNCON
	BRA.S   90$
10$
	CMP.B   #4,D1
	BEQ.S   20$
	PULL_L  D0/D1/A0/A3        ; This is the CLEAN exit.
	RET
20$
	DO	P.BTN
	DO	ISARE
	MSG	ASLEP

* ... and this is the grubby one

90$     
	PULL_L  D0/D1/A0/A3/A2     ; Ugly pop of an RET as well!
	RET



	END
