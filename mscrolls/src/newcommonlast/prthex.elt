
***********************************************************
*
*	%W%
*	%G%
*
************************************************************

		SECTION	"ADV",CODE

		include	"equates.i"
		include	"macros.i"

	IFNE     YCHEAT

		XDEF	PRTHEX,PRTHEX2
		XDEF	PHEXBYTE,PHEXWORD,PrtHexByte

  
M.HEXTBL
	DC.B     '0123456789ABCDEF'

PRTHEX

	PUSH_L	D1/D4-D5/A3
	MOVE.L  #28,D5 
	CALL_S	PRTHEX2
	PULL_L	D1/D4-D5/A3
	RET
  
PRTHEX2
 
	MOVE.L  D6,D4
	CALL_S  90$
	SUBQ.L  #4,D5
	BNE     PRTHEX2
	MOVE.L  D6,D4
	BRA.S   95$
  
90$
	LSR.L   D5,D4  
95$
	AND.W   #$F,D4
	LEA     M.HEXTBL(PC),A3
	MOVE.B  0(A3,D4.W),D1 
	DOGO	CHAROUT
 
PHEXBYTE
 
	MOVEQ   #4,D5
	BRA.S   PRTHEX2
 
PHEXWORD
 
	MOVEQ   #12,D5
	BRA.S   PRTHEX2
 

PrtHexByte

	PUSH_L 	D0-D7/A0-A6
	CALL	PHEXBYTE
	PULL_L 	D0-D7/A0-A6
	RET

	ENDC	;YCHEAT


	END
