**************************************************************
*
*	10/27/87
*	@(#)p_nonly.elt	1.4
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"scenequ.i"
	include	"linemacros.i"

*--------------------------------

	XDEF	P.NONLY
	XREF	SPACE,NOTRAIL,P.QUANT,LIST.N
	        
P.NONLY
	
*PRINT JUST QP+ NOUN
	
*ENTRY:
* D0= NOUN#
	
* USES D1
* PRESERVES D0/A0
	
	PUSH_L  D0/D3/A0/A5
	GETINFO				;TO A0
	MOVE.L  A0,A5
	TST_NPC				;NPC?
	BEQ.S   01$			;NO
	DO	GETNPC			;TO A3
01$
      IFND	wantBIGPROG
	LEA     LIST.N(A4),A0
      ENDC

      IFD	wantBIGPROG
	LEA     LIST.N,A0
      ENDC

	SUBQ.W  #1,D0
	BEQ.S   09$			;IST ONE
05$
      IFEQ	FastBits

	TEST_B  (A0)+
	BPL.S   05$			;SKIP ONE WORD
	SUBQ.W  #1,D0
	BNE.S   05$			;UNTIL TXT FOUND

      ENDC

      IFNE	FastBits

	EXG	A0,A1			;WORDSCAN wants A1
	EXG	D0,D2			;and D2

	WordScan

	EXG	A0,A1
	EXG	D0,D2

      ENDC
09$
    IFND	wantBIGPROG
	MOVE.B  (A0),D1			;GET 1ST
    ENDC	;!wantBIGPROG

    IFD		wantBIGPROG
	EXG	A0,A1
	GetWTabByte			;move.b (a1),d1
	EXG	A0,A1
    ENDC	;wantBIGPROG

	BeSure	M.ANY

	MOVE.W  #M.ANY,D3		;INCASE PLURAL
	DO	P.QUANT

    IFND	wantBIGPROG
10$
	MOVE.B  (A0)+,D1		;GET CHAR 
	BMI.S   20$			;LAST ONE
	DO	CHAROUT
	BRA.S   10$			;NEXT
20$
	DO	CHAROUT
    ENDC	;!wantBIGPROG


    IFD	wantBIGPROG

	XREF	CHANNEL

	EXG	A0,A1
	MOVE.B	CHANNEL(A4),D1
	PrtWTabWord
	EXG	A0,A1
    
    ENDC	;wantBIGPROG

	PULL_L  D0/D3/A0/A5
	TEST_B  NOTRAIL(A4)		;REQ A SPACE?
	BEQ     SPACE			;YES
	RET


	END
