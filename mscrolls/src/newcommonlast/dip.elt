**************************************************************
*
*	4/28/89
*	@(#)dip.elt	1.3
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"verbequ.i"
	include	"scenequ.i"


*-----------------------


	IfVerb	DIP

	XDEF	V.DIP

	XREF	CONJ,WHOOPS,YCANTNOW,YCANT,EXGNOUN,GET_CONT,DBS
	XREF	P.MHAVSTOP,VERB

 
V.DIP
	
	MOVE.W	CONJ(A4),D1		;any?
	BMI.S	10$			;nope, do chk4pt
	CMP.B	#IN,D1			;dip x in y?
	BEQ.S	15$			;yep, fine
	CMP.B	#INTO,D1		;or dip x into y
	BEQ.S	15$			;yep, fine
	ST	WHOOPS(A4)		;kill multiples
	MSGGO	IDFY			;'I don't follow you.'
10$
	MOVEQ	#INTO,D1		;'into what?'
15$
	DO	CHK4PT			;won't return
20$
	DO	CARRIED			;must have it
	BNE	P.MHAVSTOP		;'You must have the <xyzzy>.'
	DO	FIXCHK			;fixed somehow?
	BNE	YCANTNOW		;yep; not at the moment then

    IFD	attribute_DIPPABLE
	XREF	Denotation_DIPPABLE
	
	MOVE.L	A1,-(A7)		;stash
	LEA	Denotation_DIPPABLE(A4),A1	;A1->attribute data
	DO	ISATTRIBUTE		;EQ => D0 has this attribute
	MOVE.L	(A7)+,A1		;restore (flags intact)
    ENDC	;attribute_DIPPABLE

    IFND attribute_DIPPABLE
	TST_WORNBYPLAYER		;wearing it?
	BNE	YCANTNOW		;not at the moment then
	TST_INTRINSIC			;hmmm, 'dip feet in river'????
	BNE	YCANT			;yeah, but it might be yer head!
	TST_INSEPARABLE			;inseparably related?
	BNE	YCANT			;yep; you can't do that
	TST_MOVEABLE			;must be movable too
	BEQ	YCANT			;uh uh
	LIQUID	D1			;dip a liquid in/into summit????
	BEQ	DBS			;no way, Jose
	TST_PSEUDO			;a pseudo?
    ENDC	;!attribute_DIPPABLE
	
	BNE	YCANT			;no pseudos/non-dippables
	CALL	EXGNOUN			;flip da nouns
	
    IFD	attribute_DIPPINABLE
	XREF	Denotation_DIPPINABLE
	
	MOVE.L	A1,-(A7)		;stash
	LEA	Denotation_DIPPINABLE(A4),A1	;A1->attribute data
	DO	ISATTRIBUTE		;EQ => D0 has this attribute
	MOVE.L	(A7)+,A1		;restore (flags intact)
	BNE	D0IsNotSuitable		;that's no good (implied RET)
    ENDC	;attribute_DIPPINABLE

	LIQUID	D1			;dip summit in/into a liquid?
	BNE.S	30$			;no
	BSET	#5,3(A5)		;original DO gets wet
	BRA.S	60$
30$
	MOVE.B  10(A0),D1		;is it a container?
	AND.B   #7,D1			;sets flags (Z set if NOT a container)

    IFD	attribute_DIPPINABLE
	BNE.S	35$			;yes, process as usual
	TST_WET				;is IO wet?
	BEQ.S	60$			;no, but allow it coz it's 'dippinable'
	BSET	#5,3(A5)		;set original DO as wet
	BRA.S	60$			;carry on
35$
    ENDC	;attribute_DIPPINABLE
	
    IFND attribute_DIPPINABLE	
	BEQ	D0IsNotSuitable		;that's no good (implied RET)
    ENDC	;!attribute_DIPPINABLE
	TST_CLOSED			;container closed?
	BEQ.S	40$			;no, fine
	DO	P.BTN			;'But the NOUN '
	DO	ISARE			;'is/are '
	MSGGO	CLSE			;'closed.'
40$
	MOVE.B	2(A5),D1		;size;weight if IO
	LSR.B	#4,D1			;size
	MOVE.B	2(A0),D2		;size and weight if DO
	LSR.B	#4,D2			;size
	CMP.B	D2,D1			;too big?
	BLT.S	45$			;no, ok
	CALL	EXGNOUN			;swap wossnames
	DO	P.BTN			;'but the <DO>'
	DO	ISARE			;'<is/are>'
	MSG	TOOBIG			;'too big'
	DOGO	BSFULLSTOP		;'.'; and exit
45$
	PUSH_L	D0/A0			;stash
	CALL	GET_CONT		;of D0. A1 -> zterm list of contents
50$
	MOVE.W	(A1)+,D0		;get one
	BMI.S	50$			;void
	BEQ.S	55$			;eolist
	GETINFO				;-> A0
	LIQUID	D1			;is it a liquid?
	BNE.S	50$			;no, next
	BSET	#5,3(A5)		;original DO gets wet
55$
	PULL_L	D0/A0
60$
	CALL	EXGNOUN

    IFD	wantSP.DIP
	XREF	SP.DIP
	
	CALL	SP.DIP
    ENDC	;SP.DIP

	DO	P.SUB			;You/he/she etc
	MOVE.W	VERB(A4),D1
	DO	P.VERB			;dip
	DO	P.TN			;the DO
	MOVE.W	CONJ(A4),D1
	DO	P.CONJ			;in/into
	CALL	EXGNOUN
	DOGO	P.TNSTOP		;the IO
	

*-----------------------


*
* D0IsNotSuitable	-	simply prints 'The <NOUN> is not
*				suitable.'
*

D0IsNotSuitable

	DO	P.TN			;'The <wossname> '
	DO	ISARE			;'is/are '
	MSG	TSOFT			;'not suitable'
	DOGO	FULLSTOP


*-----------------------

	ENDC	;ifverb
	
	END

