******************************************************
*
*	@(#)suss.elt	2.8
*	5/25/88
*
******************************************************

		SECTION		"ADV",CODE

* AID TO DEBUGGING

	include	"equates.i"
	include	"macros.i"
	include	"nounequ1.i"
	include	"nounequ2.i"
	include "scenequ.i"

 
      XREF     P.TN,LINE,CHAROUT,GETNPC,IGNORAJ
      XREF     HAVEWON,W.IDFY,USER.DB,P.NOUN,VERB,VERB2,ADDNOUN,CONJ
      XREF     GETDATA,GETVECT,PRTTXT


      XDEF     V.CHEAT

     IFNE     YCHEAT

      XDEF     SUSS,V.FOD,V.LOOP
      XREF     DBUG.ON,CHEAT,CHEAT.LI,CHEAT2,CHEATNUM
	XREF	R.NOUN2,PSUDAT


LONG	MACRO	* routine name
	IFND	\1
	XREF	\1
	ENDC
	PEA	*+14(PC)		;return address
	MOVE.L	A4,-(A7)		;base addr
	ADD.L	#\1,(A7)		;routine's offset
	RET
	ENDM



TXT	MACRO
	DC.B	'\1 ',0
	ENDM

SUSS
	TEST_W  DBUG.ON(A4)
	BEQ     90$
	PUSH_L  A0/D0
	MSG    LINE
	LONG	P.TN
	MOVEQ   #'#',D1
	LONG	CHAROUT
	MOVE.W  D0,D6
	LONG    PHEXWORD
	MSG    LINE
	MOVE.L  A0,D6
	LONG    PRTHEX
	MSG    LINE
	MOVE.B  #6,D2
10$
	MOVE.B  (A0)+,D6
	LONG    PHEXBYTE
	MSG    SPACE
	SUBQ.B  #1,D2
	BNE.S   10$
	MSG    LINE
	MOVE.L  (A0)+,D6
	LONG    PRTHEX
	MSG    SPACE
	MOVE.W  (A0)+,D6
	LONG    PHEXWORD
	MSG    SPACE
	MOVE.W  (A0)+,D6
	LONG    PHEXWORD
	MSG    LINE
	PULL_L  A0/D0
	PUSH_L 	D0/A0
	TEST_W	IGNORAJ(A4)	 ;quietly/and adverb?
	BPL.S	20$
	CALL	LONG_SUSS
20$
	PULL_L 	D0/A0
	TST_NPC				;
	BEQ.S   90$
	LONG	GETNPC	              ;A3
	MOVE.L  A3,D6
	LONG    PRTHEX
	PRINT   <': '>
	LONG	GETNPC
	MOVE.L  (A3),D6
	LONG    PRTHEX
90$
	RET
 
	ENDC

*--------------------------------
  
  
V.CHEAT
 
    IFNE	YCHEAT

	TEST_W  DBUG.ON(A4)
	BEQ     90$

   IFNE	THE_PAWN
	CMP.W   #NBAND,D0          ;CHEAT BAND?
   ENDC 

   IFEQ	THE_PAWN
	CMP.W	#DEBUG_BAG,D0
   ENDC

	BNE.S   10$
	PRINT   <'OK THEN'>
	NOT.W   CHEAT2(A4)
	RET
10$ 

   IFNE	THE_PAWN
	CMP.W   #DEBUG_SHIRT,D0
   ENDC 

   IFEQ	THE_PAWN
	CMP.W	#DEBUG_SHIRT,D0
   ENDC

	BNE.S   20$  
	PRINT   <'...'>
	NOT.W   CHEAT.LI(A4)
	RET
20$
	CMP.W    #DEBUG_JEANS,D0
	BNE.S    30$
	PRINT    <'!!!'>
	NOT.W    CHEATNUM(A4)
	RET
30$
    IFD	wantMOBILES
    IFD	wantDebugMe
	XREF	SNOOP,PRTHEX,DoYouKnow
	CMP.W	#NPLAYER,D0
	BNE.S	31$
	MOVEQ	#1,D1				;bits start from 1 here.
42$
	CALL	DoYouKnow
	BEQ.S	40$
	PRINT	<'1'>
	BRA.S	41$				;next
40$
	PRINT	<'0'>
41$
	ADDQ.W	#1,D1				;inc bit count.
	CMP.W	#20,D1				;last
	BLS.S	42$				;continue.
	MSG	LINE
	NOT.W	SNOOP(A4)
	PRINT	<'snoop snoop ...^'>
	RET
31$
    ENDC	;wantDebugMe
    IFD	DEBUG_LEGS
	XREF	SP.DebugLegs
	CMP.W	#DEBUG_LEGS,D0			;debug legs!!
	BNE.S	33$
	LJUMP	SP.DebugLegs			;so it can change
33$
    ENDC	;DEBUG_LEGS
    ENDC	;wantMOBILES

	PRINT   <'HMM'>
	NOT.W   CHEAT(A4)
	RET
90$
 
	ENDC	;YCHEAT

      IFNE	THE_PAWN
  
	TEST_W  HAVEWON(A4)        ;WON YET?
	BNE.S	9001$
      
      ENDC

	MSGGO	IDFY             ;HONEST! 

      IFNE	THE_PAWN
9001$
	NOT.W   USER.DB(A4)        ;OK LET EM TOGGLE   
	RET
      
      ENDC

*--------------------------------
 
	IFNE     YCHEAT
	  
V.FOD
 
	TEST_W  DBUG.ON(A4)
	BEQ     90$
	BSET    #1,(A0)      ;DEAD TOO
	MSG	LINE
	LONG	P.NOUN
	PRINT   <' NOW DECEASED'>
90$
	RET
 
*--------------------------------
	
V.LOOP
	
	MOVE.W  VERB2(A4),VERB(A4) ;REAL VERB
	MOVE.W  D5,D3              ;TARGET
	PUSH_L  D0/A0  
	MOVE.W  ADDNOUN+2(A4),D0
	MOVE.W  CONJ+2(A4),CONJ(A4)
	GETINFO        
	MOVE.L  D0,D5
	MOVE.L  A0,A5
	PULL_L  D0/A0
10$
	CMP.W   D3,D0              ;THERE YET?
	BHI.S   20$                ;YES, DONW
	PUSH_L  D0-D1/D3/D5/A0/A5
	MOVE.W  VERB(A4),D2
	LONG	GETVECT            ;TO A1
	JSR     (A1)               ;DO IT
	PULL_L  D0-D1/D3/D5/A0/A5
	MSG    LINE
	ADDQ.W  #1,D0              ;NEXY
	GETINFO        
	MOVE.B  (A0),D1
	BRA.S   10$                ;OK?
20$
	PRINT   <'DONE^'>
	RET     
	
	ENDC	;YCHEAT
   

	IFNE	YCHEAT

LONG_SUSS

* noun states (status)

      IFNE	M68000
	RET
      ENDC

      IFEQ	M68000

BYTE0
	MSG	LINE
	TST_LIT				;lit?
	BEQ	10$
	PRINT	<'Lit '>
10$
	TST_LOCKED				;locked?
	BEQ	20$
	PRINT	<'Locked '>
20$
	TST_CLOSED				;
	BEQ	30$
	PRINT	<'Closed '>
30$
	TST_OPEN				;
	BEQ	40$
	PRINT	<'Open '>
40$
	TST_LOCALCONTENTS				;
	BEQ	50$
	PRINT	<'Get without opening '>
50$
	TST_BROKEN				;
	BEQ	60$
	PRINT	<'Broken '>
60$
	TST_DEAD				;
	BEQ	70$
	PRINT	<'Dead NPC '>
70$
	TST_PLURAL				;
	BEQ	80$
	PRINT	<'Plural '>
80$

* Physical characteristics

BYTE1_T

	MOVE.B	1(A0),D6
	LSR.B	#4,D6
	AND.W	#$F,D6
	BEQ	BYTE1_B
	PRINT	<'^Hardness = '>
	LONG	PrtHexByte
	PRINT	<' = '>
	LEA	BYTE1_T_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE1_B
	LONG	CHAROUT
	BRA.S	30$

BYTE1_T_Text

	TXT	Void
	TXT	Liquid
	TXT	Quantity
	TXT	<Can be cut with a sharp object>
	TXT	<Can be cut with a very sharp object>
	TXT	Normal
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	Brittle
	TXT	Tool
	TXT	<Can be tied>
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	<Unbreakable (and cannot break things!)>

	ALIGN

BYTE1_B

	MOVE.B	1(A0),D6
	AND.W	#$F,D6
	BEQ	BYTE2_T
	PRINT	<'^Strength = '>
	LONG	PrtHexByte

BYTE2_T

	MOVE.B	2(A0),D6
	LSR.B	#4,D6
	AND.W	#$F,D6
	BEQ	BYTE2_B
	PRINT	<'^Size = '>
	LONG	PrtHexByte
	TST_ISAROOM				;room?
	BEQ	BYTE2_B
	PRINT	<' = '>
	LEA	BYTE2_T_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE2_B
	LONG	CHAROUT
	BRA.S	30$

BYTE2_T_Text

	TXT	Normal
	TXT	<Squeeze, you must drop everything>
	TXT	<Small, only large enough for one person>
	TXT	<Low - no vehicles>
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	<Whack, you hit your head!>
	TXT	<The [vehicle] stumbles>
	TXT	<Ridiculous to ride there>
	TXT	UNDEFINED
	TXT	UNDEFINED
	TXT	UNDEFINED

	ALIGN

BYTE2_B

	MOVE.B	2(A0),D6
	AND.W	#$F,D6
	PRINT	<'^Weight = '>
	LONG	PrtHexByte

BYTE3_T

	MOVE.B	3(A0),D6
	LSR.B	#6,D6
	AND.W	#$3,D6
	BEQ	BYTE3_T_bits
	PRINT	<'^Sharpness = '>
	LONG	PrtHexByte
	PRINT	<' = '>
	LEA	BYTE3_T_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE3_T_bits
	LONG	CHAROUT
	BRA.S	30$

BYTE3_T_Text

	TXT	N/A
	TXT	Blunt
	TXT	Sharp
	TXT	<Very sharp>

	ALIGN

BYTE3_T_bits

	MSG	LINE
	TST_WET				;wet?
	BEQ	10$
	PRINT	<'Wet '>
10$	
	TST_MOVEABLE				;
	BEQ	20$
	PRINT	<'Moveable '>
	BRA	30$
20$
	PRINT	<'Steadfastly refuses to be moved '>
30$

* texture

BYTE3_B

	MOVE.B	3(A0),D6
	AND.W	#$F,D6
	BEQ	BYTE4_T
	PRINT	<'^Texture = '>
	LONG	PrtHexByte
	PRINT	<' = '>
	LEA	BYTE3_B_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE4_T
	LONG	CHAROUT
	BRA.S	30$

BYTE3_B_Text

	TXT	Void
	TXT	Smooth	
	TXT	Rough
	TXT	Hot
	TXT	Cold
	TXT	Slippery
	TXT	Furry
	TXT	Clammy
	TXT	Soft
	TXT	Hard
	TXT	Warm
	TXT	NPC
	TXT	Thorny
	TXT	Sticky
	TXT	UNDEFINED
	TXT	UNDEFINED

	ALIGN

BYTE4_T

	MSG	LINE
	TST_LIGHTABLE				;lightable?
	BEQ	10$
	PRINT	<'Lightable '>
10$
	TST_NPC				;
	BEQ	20$
	PRINT	<'Is an NPC '>
20$
	TST_NPCREFUSES				;
	BEQ	30$
	PRINT	<'NPC refuses to use object '>
30$
	TST_EXPLORED				;
	BEQ	40$
	PRINT	<'Explored '>
40$
	TST_MONEY				;
	BEQ	50$
	PRINT	<'Money '>
50$
	MOVE.B	4(A0),D6
	AND.W	#7,D6
	BEQ	60$
	PRINT	<'Value = '>
	LONG	PrtHexByte
60$

BYTE5

	MSG	LINE
	BTST	#7,5(A0)		;Slow ROOTNUM?
	BEQ	10$
	PRINT	<'Slow ROOTNUM '>
10$
	BTST	#6,5(A0)		;NPC will give away ?
	BEQ	20$
	PRINT	<'NPC will give away  '>
20$
	BTST	#5,5(A0)
	BEQ	30$
	PRINT	<'Force plural '>
30$
	BTST	#4,5(A0)
	BEQ	40$
	PRINT	<'Force adjective '>
40$
	TST_SITTABLE				;
	BEQ	50$
	PRINT	<'Can be sat on '>
50$
	TST_LITROOM				;
	BEQ	60$
	PRINT	<'Lit room '>
60$
	TST_INITIALDONE				;
	BEQ	70$
	PRINT	<'Initial state done '>
70$
	TST_PSEUDO				;
	BEQ	80$
	PRINT	<'Pseudo '>
80$

BYTE6
	MSG	LINE
	PRINT	<'LOCATION INFO: '>
	TST_PSEUDO				;pseudo?
	BEQ	BYTE6_main
*
* this may take forever!
*
	PRINT	<'^Pseudo item in: '>

	LEA	PSUDAT(A4),A2		;get start of pseudos list
	ADD.W	8(A0),A2		;get the first one for this noun
10$
	MOVE.B	(A2)+,D2		;get room no.
	BEQ.S	20$
	AND.W	#$FF,D2
	LONG	R.NOUN2			;noun# to D0
	LONG	P.NOUN
	PRINT	'('
	MOVE.W	D2,D6
	LONG	PrtHexByte
	PRINT	<') '>
	BRA	10$
20$
	MSG	LINE
	BRA	BYTE10

BYTE6_main

	TST_OUT				;out?
	BEQ.S	10$
	PRINT	<'Out '>
10$
	TST_ISAROOM				;room?
	BEQ	20$
	PRINT	<'is a room'>
	BRA	BYTE10
20$
	MOVE.B	6(A0),D1
	AND.B	#$7F,D1
	BNE	NotSimple
	MOVE.W	8(A0),D2
	BNE	25$
	PRINT	<'inventory'>
	BRA	BYTE10
25$
	PRINT	<'Simply in room: '>
	LONG	R.NOUN2
	LONG	P.NOUN
	BRA	BYTE10

NotSimple


	TST_INSEPARABLE				;inseparable?
	BEQ	10$
	PRINT	<'inseparable '>
10$
	TST_CONTAINED				;
	BEQ	20$
	PRINT	<'Contained in: '>
20$
	BTST	#0,6(A0)
	BEQ	30$
	PRINT	<'Related to: '>
30$
	TST_HIDDEN				;
	BEQ	40$
	PRINT	<'Hidden by: '>
40$
	TST_INTRINSIC				;intrinsic
	BEQ	50$
	PRINT	<'Intrinsic '>
50$
	TST_WORNBYPLAYER				;worn
	BEQ	60$
	PRINT	<'Worn by player '>
60$
	MOVE.B	6(A0),D1
	AND.B	#$0C,D1			;worn/intrinsic mask
	BEQ	70$
	TEST_B	7(A0)			;secondry locator
	BEQ	65$
	PRINT	<'!* invalid secondary locator *! '>
65$
	TEST_W	8(A0)			;location must be zero
	BEQ	BYTE10
	PRINT	<'!* invalid location *! '>
	BRA	BYTE10
70$
	MOVE.L	D0,D5
	MOVE.L	A0,A5

	MOVE.W	8(A0),D0
	GETINFO
	LONG	P.TN
	TEST_B	7(A5)		;secondary loc. of orig noun
	BEQ	80$		;0=> ok
	TST_PSEUDO				;else its loc must be a PSEUDO (probably!)
	BNE.S	75$
	PRINT	<'!* marginal secondary locator *!'>
75$
	PUSH_L 	D5/A5
	MOVE.B	7(A5),D2
	AND.W	#$FF,D2
	LONG	R.NOUN2
	PRINT	<'in room: '>
	LONG	P.NOUN
	PULL_L 	D5/A5
80$
	MOVE.L	D5,D0
	MOVE.L	A5,A0

BYTE10
	
	MSG	LINE
	MOVE.B	10(A0),D1
	AND.B	#$F8,D1			;mask clothing
	BEQ	container_vol
	PRINT	<'Clothing - covers: '>
	BTST	#7,D1
	BEQ	10$
	PRINT	<'feet & legs '>
10$
	BTST	#6,D1
	BEQ	20$
	PRINT	<'groin '>
20$
	BTST	#5,D1
	BEQ	30$
	PRINT	<'arms '>
30$
	BTST	#4,D1
	BEQ	40$
	PRINT	<'head '>
40$
	BTST	#3,D1
	BEQ	50$
	PRINT	<'chest '>
50$

container_vol

	MSG	LINE
	MOVE.B	10(A0),D6
	AND.B	#7,D6
	BEQ	90$		;not a container
	SUBQ.B	#1,D6
	PRINT	<'Container volume '>
	LONG	PrtHexByte
90$

BYTE11

	MSG	LINE
	MOVE.B	11(A0),D6
	LSR.B	#5,D6
	BEQ	BYTE11_M
	PRINT	<'^Edibility = '>
	MOVE.W	D6,-(A7)
	LONG	PrtHexByte
	MOVE.W	(A7)+,D6
	PRINT	<' = '>
	LEA	BYTE11_T_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE11_M
	LONG	CHAROUT
	BRA.S	30$

BYTE11_T_Text

	TXT	<Not edible>
	TXT	Fatal
	TXT	Poisoned
	TXT	<Become ill>
	TXT	Edible
	TXT	Tasty
	TXT	<Very good>
	TXT	<Magical (healing properties)>

	ALIGN



BYTE11_M

	MOVE.B	11(A0),D6
	LSR.B	#3,D6
	AND.W	#3,D6
	BEQ	BYTE11_B
	PRINT	<'^Combustability = '>
	MOVE.W	D6,-(A7)
	LONG	PrtHexByte
	MOVE.W	(A7)+,D6
	PRINT	<' = '>
	LEA	BYTE11_M_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE11_B
	LONG	CHAROUT
	BRA.S	30$

BYTE11_M_Text

	TXT	<Will not burn>
	TXT	<Burns away>
	TXT	Melts
	TXT	Explodes

	ALIGN

BYTE11_B

	MOVE.B	11(A0),D6
	LSR.B	#1,D6
	AND.W	#3,D6
	BEQ	BYTE11_bit0
	PRINT	<'^Wearability = '>
	MOVE.W	D6,-(A7)
	LONG	PrtHexByte
	MOVE.W	(A7)+,D6
	PRINT	<' = '>
	LEA	BYTE11_B_Text(PC),A1
10$
	SUBQ.B	#1,D6
	BMI.S	30$
20$
	TEST_B	(A1)+
	BNE.S	20$
	BRA.S	10$
30$
	MOVE.B	(A1)+,D1
	BEQ	BYTE11_bit0
	LONG	CHAROUT
	BRA.S	30$

BYTE11_B_Text

	TXT	<Cannot wear>
	TXT	Clothing
	TXT	Protective
	TXT	<Extra protective>

	ALIGN

BYTE11_bit0

	BTST	#0,11(A0)
	BEQ	10$
	PRINT	<'^Worn by npc'>
10$

WORD12

	MSG	LINE
	BTST	#7,12(A0)		;permanently fixed?
	BEQ	10$
	PRINT	<'Permanently fixed to '>
10$
	MOVE.W	12(A0),D0
	AND.W	#$7FFF,D0
	BEQ	90$
	LONG	P.TN
90$
	MSG	LINE
	RET

	ENDC	;!68000

	ENDC	;YCHEAT





      END

