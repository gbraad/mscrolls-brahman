**************************************************************
*
*	10/27/87
*	@(#)dirnpc.elt	1.3
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"verbequ.i"


*--------------------------------

	XDEF	DIRNPC
	XREF	NOUN,INCNOUN,NPCINRM,WRDPNT,VERB,SPIEL
	XREF	DEC.N,BACK.WRD
	XREF	PVERB,PADV,ADVERB
	
DIRNPC

* are speaking directly to a character via "eppy, how are you?" etc
* BUT we could be hung over from;
* >hit
* hit what?
* >eppy
* OR
* >eppy how are you
* need to decide whether we should break from previous cmd or continue
	
	CLR.W	NOUN(A4)
	CALL	INCNOUN			;INTO BUFFER
	DO	NOERR			;MASK ERRORS
	MOVE.W  D0,NPCINRM(A4)		;SET AS FOUND
	MOVE.L  A6,WRDPNT(A4)		;TEXT START IF NOT THE CASE
	DO	GETWRD			;FOR COMMA
	CMP.B	#1,D7			;eol?
	BEQ.S	50$			;yes, trivial or continuation
	CMP.B   #SECT.SPEC,D6 
	BNE.S   20$			;NO COMMA
	TEST_W  D5			;0= AND?
	BNE.S   20$			;NO 
	MOVE.L  A6,WRDPNT(A4)		;UPDATE SINCE COMMA
	DO	GETWRD			;NEXT
20$
	MOVE.W  #VNASK,VERB(A4)		;INCASE of 'kronos, about band'
	BRA	SPIEL
50$
	TEST_B	PVERB(A4)		;are we following a hanging verb?
	BMI.S	20$ 			;no, must be trivial, exit
	CLR.W	(A3)			;preserved nounbuf ptr, clear it
	CLR.W	NOUN(A4)		;remove counter
	DO	BACK.WRD		;back over noun for mnph
	DO	YESERR
	MOVEQ	#0,D7			;clear any error
	MOVE.W	PVERB(A4),VERB(A4)
	MOVE.W	PADV(A4),ADVERB(A4)	;set'em up
	BRA	DEC.N			;jump in at nph stage & continue

	END
