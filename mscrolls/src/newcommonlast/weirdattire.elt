***********************************************************
*
*	%W%
*	%G%
*
************************************************************

		SECTION	"ADV",CODE

		include	"equates.i"
		include	"macros.i"
		include	"scenequ.i"

		IFD	attribute_WEIRDATTIRE

		XDEF	WeirdAttire

WeirdAttire

		XREF	GETRELN2,ISATTRIBUTE
		XREF	Denotation_WEIRDATTIRE

* Routine to determine whether the player is attired weirdly
* Exit
* 	EQ <=> not weird.
*	NE => clothing mask overlay of weird attire area(s) in d1

* Sometimes player clothing has no mask (the case for always displayed
* garments), in these cases no specific area can be found. The hi-bit
* of d1 is then set, to flag this event and to return a non-zero result

* 	STOMPS D1

	PUSH_L	D0/D2-D4/A0-A2
	CLR.W	D2		;target is player
	DO	GETRELN2	;related to player (wil be local anyway)->a1
	MOVE.L	A1,A2		;use a2 as pointer
	LEA	Denotation_WEIRDATTIRE(A4),A1	;data pointer
	CLR.B	D1		;clothing mask of weird things
	CLR.B	D3		;clothing mask of things worn.
10$
	MOVE.W	(A2)+,D0	;fetch object
	BEQ.S	20$		;all done
	BMI.S	10$		;not included
	GETINFO
	TST_WORNBYPLAYER	;is the player wearing it?
	BEQ.S	10$		;no try another
	MOVE.B	10(A0),D2	;get mask
	LSR.B	#3,D2		;shift away container vol
	BEQ.S	14$		;if null then local.
	MOVE.B	D3,D4		;temp store of current mask.
	OR.B	D2,D3		;or in clothing cover.
	CMP.B	D3,D4		;did this garment change the mask
	BEQ.S	10$		;no, hence garment is covered and not seen.
14$
	DO	ISATTRIBUTE	;is this garment weird?
	BNE.S	10$		;no, look again
	MOVE.B	10(A0),D2	;get mask
	LSR.B	#3,D2		;shift away container vol
	BNE.S	15$		;non-zero OR in.
	BSET	#7,D2		;otherwise hi-bit flag
15$
	OR.B	D2,D1		;or into weird mask
	BRA.S	10$		;try for anymore
20$

	; at this point, D3 is your clothing mask
	; ie mask of body areas covered by garments
	; and
	; D1 is the body mask of weird garments that can be seen.

    IFD	wantSP.WEIRDATTIRE
	XREF	SP.WEIRDATTIRE
	CALL	SP.WEIRDATTIRE
    ENDC	;wantSP.WEIRDATTIRE


	BTST	#3,D3		;covering the naughty bits?
	BNE.S	30$		;yes, that is normal
	BSET	#3,D1		;else that area is weird.
30$
	TEST_B	D1		;set flags on result
	PULL_L	D0/D2-D4/A0-A2
	RET
	
*------------------------------------------
	

	ENDC	;attribute_WEIRDATTIRE



	END
