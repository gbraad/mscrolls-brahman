**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include "nounequ1.i"
	include "scenequ.i"

    IFD		wanted_OWNERSHIP

	XDEF	OWNERSHIP,GETOWNER,OWNERLOCAL

	XREF	TBL.OWNERSHIP,POSSESS,VALID.NP

*-------------------------------------------------
* OWNERSHIP,
*
* Entry:	D0= item owned.
*		POSSESS= person who may be owning
* Exit:
*		NE  => no such ownership
*		EQ  => yes the POSSESS owns the D0

OWNERSHIP

	PUSH_L	A0/D1
	LEA	TBL.OWNERSHIP(A4),A0		;start of table
10$
	MOVE.W	(A0)+,D1			;get owner
	BEQ.S	50$				;e o list, fail
	CMP.W	POSSESS(A4),D1			;this owner?
	BEQ.S	20$				;yes, find out
15$
	TEST_W	(A0)+				;e o this owner list?
	BNE.S	15$				;no, find end
	BRA.S	10$				;ok, try again
20$
	MOVE.W	(A0)+,D1			;get item
	BEQ.S	50$				;fail
	CMP.W	D1,D0				;owns this item?
	BEQ.S	90$				;yes, return sucess.
	BRA.S	20$				;no, try next.
50$
	SETNE					;fail
90$
	PULL_L	A0/D1
	RET

*-------------------------------------------------

GETOWNER

* Fetch owner from ownership table generated by fred.
* Entry:	D0 = item
* Exit:		D1 = owner ( 0 => not owned )
*		(flags on d1)

	PUSH_L	D0/D2/A0-A1
	LEA	TBL.OWNERSHIP(A4),A1		;start of table
10$
	MOVE.W	(A1)+,D1			;owner.
	BEQ.S	20$				;eo table, done
15$
	MOVE.W	(A1)+,D2			;get item
	BEQ.S	10$				;oe list, try next person
	CMP.W	D2,D0				;is it the item??
	BNE.S	15$				;no, continue	
20$
	TEST_W	D1				;set flags on result
	PULL_L	D0/D2/A0-A1			;restore registers
	RET

*-------------------------------------------------

OWNERLOCAL

* detect if the owner is local (NB: if not owned then not local)
* Entry:	D0 = item
* Exit   	EQ=> local
*		NE=> not local

	PUSH_L	D0/D1			;save'em
	DO	GETOWNER		;to d1 (if exists)
	BNE.S	10$			;ok
	SETNE				;fail
	BRA.S	90$			;exit
10$
	MOVE.W	D1,D0			;fetch
	DO	VALID.NP		;local?
90$
	PULL_L	D0/D1			;restore
	RET

*-------------------------------------------------

	XDEF	GETNPCOWNER

GETNPCOWNER

* Gets the owner, but only if it is an npc.

	PUSH_L	D0/A0
	CALL_S	GETOWNER		;-> D1
	BEQ.S	90$			;none
	MOVE.W	D1,D0
	GETINFO
	TST_NPC				;is it an npc?
	BNE.S	80$			;yes, ok
	CLR.W	D1			;else no good
80$
	TEST_W	D1			;set flags
90$
	PULL_L	D0/A0
	RET

*------------------------------------------


    ENDC	;wanted_ownership

	END
