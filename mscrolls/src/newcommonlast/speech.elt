**************************************************************
*
*	3/13/89
*	@(#)speech.elt	2.5
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"verbequ.i"
	include	"scenequ.i"

*--------------------------------

	XDEF	SPEECH,SPIEL

	XREF	NOUN,WRDPNT,VERB,MNPH,NPCINRM,VERB
	XREF	NearNPC,INCNOUN,TEMPBUF,DECODE,IFANIMAL,FORWHAT
	XREF	ERR4,GLOBAL,NOTWSYN,QUOTES,IsNextNoun
  
SPEECH
  
* to avoid:
* >g
* what do u want to get?
* say piss off
* bomb!!!!!!!!!!
*
* due to address error when noun =1 (really noun is a pointer - I'll get
* Hugh one day for this)

	CLR.W	NOUN(A4)		;yuk! 

    IFND	wantLocalInanimateObjects
	DO	NOERR
    ENDC	;!wantLocalInanimateObjects

	MOVE.L  A6,WRDPNT(A4)

      IFEQ	THE_PAWN

	XREF	SP.SPEECH

	CALL	SP.SPEECH
	BEQ	SP55			;something happened

      ENDC

	CMP.W   #VNASK,VERB(A4)
	BEQ.S   10$
	CMP.W   #VNTELL,VERB(A4)
	BEQ.S   10$
    IFD		wantLocalInanimateObjects
	DO	GETADJPH
	CMP.B	#SECT.CONJ,D6
	BNE.S	009$
	CMP.W	#1,D5
	BEQ.S	10$
009$
	DO	NOERR
	BRA.S	20$
    ENDC	;wantLocalInanimateObjects

    IFND	wantLocalInanimateObjects
	DO	GETADJPH
	CMP.B   #SECT.CONJ,D6 
	BNE.S   20$
	CMP.W   #1,D5			;to?
	BNE.S   20$
    ENDC	;!wantLocalInanimateObjects

10$
*	CALL	IsNextNoun		;incase MNPH can't cope
*	BEQ.S	40$			;EQ => yes
	DO	MNPH
	CMP.B	#2,D7
	BEQ	SP55
    IFD		wantLocalInanimateObjects
	DO	NOERR
    ENDC	;wantLocalInanimateObjects

	BRA.S   40$
19$
    IFD		wantPOSSESSIVE_PRONOUNS
	XREF	HERFLAG,PRONHER
	TEST_B	HERFLAG(A4)			;might be ask her for...
	BEQ.S	20$				;no, continue.
	MOVE.W	PRONHER(A4),D0			;we choose 'her' then.
	BRA.S	50$				;and continue with that.
    ENDC	;wantPOSSESSIVE_PRONOUNS

20$
	MOVE.W  D5,D2
	MOVE.W  NPCINRM(A4),D5
      
      IFNE	THE_PAWN

	BMI     SP50               ;NOBODY THERE

      ENDC

      IFEQ	THE_PAWN

	XREF	NearbyVerbs,VLEN_NBVerb
	
	BPL.S	30$			;we've got an NPC, no worries

* see if the verb allows NPC in next room (there's a case for a vsub here)

	PUSH_L	D0/A0			;shortage of OK regs!
	LEA	NearbyVerbs(A4),A0
	MOVEQ	#VLEN_NBVerb,D0

	MOVE.W	VERB(A4),D5
25$
	CMP.B	(A0)+,D5		;search list for verb
	DBEQ	D0,25$

	PULL_L	D0/A0			;won't kill flags

	BNE	SP50			;NE=> not in list

* ok, so the verb's allowed, but is there an NPC nearby?

	MOVE.W	NearNPC(A4),D5
	BMI	SP50			;MI=> nope
	MOVE.W	D5,NPCINRM(A4)

30$
      ENDC

	CALL    INCNOUN
	MOVE.W  D2,D5
	BRA.S   SPIEL              ;OK NEXT
40$ 
	CLR.W   NOUN(A4)           ;ZAP
	LEA     TEMPBUF(A4),A0
	MOVE.W  (A0)+,D0           ;GET NOUN#
	BEQ.S   19$
	TEST_W  (A0)+
	BEQ.S   50$
	MSG	1ATTIM
	DOGO	DECODE
50$
	GETINFO        
	TST_NPC				;ALIVE?
	BNE.S   60$                ;OL
	MSG	MAD
	DOGO	DECODE
60$
	MOVE.W  D5,D1			;save D5 (used as a flag to VALID)
	MOVEQ   #0,D5              ;MAKE SURE ERRORS HAPPEN
	DO	VALID
	BNE     DECODE
	MOVE.W  D0,NPCINRM(A4)     ;UPDATE
	MOVE.W  D0,D5
	CALL    INCNOUN
	MOVE.W  D1,D5              ;RET  NEXT TYPE
SPIEL

	MOVE.W  NPCINRM(A4),D0

      IFD	wantSP.SPIEL
	XREF	SP.SPIEL
	CALL	SP.SPIEL
	BEQ	SP55
      ENDC	;SP.SPIEL

	CALL    IFANIMAL
	BNE     SP55               ;GO BACK TO SCREEN
	CMP.W   #SECT.CONJ,D6      ;CONJ?  
	BNE.S   40$                ;NOPE
	CMP.W   #$12,D5            ;ABOUT?
	BEQ.S   30$       
	CMP.W   #6,D5              ;FOR?
	BNE.S   40$                ;NOPE
	CALL    FORWHAT            ;DO FOR CONSTRUCT
	DOGO	DECODE
	
30$                                
	DO	YESERR  
	ST      ERR4(A4)           ;EXCPT WHICH
	CALL    GLOBAL             ;EXCEPT 'I DONT SEE..' ERROR
 
	BRA     NOTWSYN            ;DO AS NORMAL
40$
	MOVE.L  WRDPNT(A4),A6
	BRA     QUOTES
SP50
	MSG	NOONE
SP55
	MOVE.L  (A7)+,D0           ;DUMP
	DOGO	SCREEN

*--------------------------------
	
	
	XDEF	INTERROR
  
INTERROR
	
	MSG	ERROR
	DOGO	SCREEN            ;HIT THAT STACK!

*--------------------------------
	        



	END
