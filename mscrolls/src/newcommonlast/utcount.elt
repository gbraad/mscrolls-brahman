**************************************************************
*
*	10/27/87
*	@(#)utcount.elt	1.4
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"scenequ.i"
	include	"linemacros.i"


    IFNE    YCHEAT

*
* DBCOUNT - returns no. of sections and words in the current word table
*
	XDEF	DBCOUNT
	XREF	TST.DBUG,TBL.WRD,PRTHEX,PHEXWORD,PHEXBYTE

DBCOUNT

	CALL    TST.DBUG
      
      IFND	wantBIGPROG

	LEA     TBL.WRD(A4),A0
      
      ENDC

      IFD	wantBIGPROG

	LEA     TBL.WRD,A0
      
      ENDC

	MOVEQ   #0,D5             ;word count
	MOVEQ   #0,D6             ;section count
	MOVEQ	#0,D4	
	MOVEQ	#0,D7
10$

      IFND	wantBIGPROG

	MOVE.B  (A0)+,D0          ;find a hibit (word terminator)
      
      ENDC

      IFD	wantBIGPROG

	EXG	A0,A1
	EXG	D0,D1
	GetWTabByte		  ;like MOVE.B (A1),D1
	EXG	A0,A1
	EXG	D0,D1

	ADDQ.L	#1,A0
	TEST_B	D0
      
      ENDC

	BPL.S   10$
	CMP.B   #$81,D0           ;end of table?
	BEQ.S   90$
	CMP.B   #$82,D0           ;end of section?
	BNE.S   20$
15$
	ADDQ.L  #1,D6             ;up the section count
	PUSH_L	D5/D6
	SUB.L	D4,D5
	PRINT	<'^Section # '>
	PUSH_L	D0-D7/A0-A6
	SUBQ.L	#1,D6
	CALL	PRTHEX
	PULL_L	D0-D7/A0-A6
	PRINT	<' words = '>
	MOVE.L	D5,D6
	PUSH_L	D0-D7/A0-A6
	CALL	PRTHEX
	PULL_L	D0-D7/A0-A6
	PULL_L	D5/D6
	MOVE.L	D5,D4

	BRA     10$
20$
	ADDQ.L  #1,D5             ;up word count
	BRA     10$
90$

      IFEQ	THE_PAWN

	BSET	#0,D7		;two table terminators
	BEQ.S	15$

      ENDC

	PRINT   <'^SECTIONS = '>
	MOVE.W  D5,-(A7)
	CALL    PHEXBYTE
	MOVE.W  (A7)+,D6
	PRINT   <'^WORDS = '>
	CALL    PHEXWORD
	DOGO	DBEXIT


      ENDC



	END
