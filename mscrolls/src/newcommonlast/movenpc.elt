**************************************************************
*
*	3/9/89
*	@(#)movenpc.elt	2.3
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"scenequ.i"

    IFND	wantNONPCPAUSE

*--------------------------------
    IFND	wantMOBILES		;too long for mobiles

* BNE: PAUSED
   
	XDEF	CHKPAUSE
   
CHKPAUSE

	PUSH_L  D0/D1/A0/A3
	TST_DEAD				;
	BNE.S   50$
	DO	GETNPC
	MOVE.B  2(A3),D1
	LSR.B   #4,D1
	CMP.B   #5,D1
	BEQ.S   50$
	CMP.B   #4,D1
	BEQ.S   50$
	BTST	#0,3(A3)		;PAUSED?
	BEQ.S   10$
50$
	MOVEQ   #1,D1			;set ne
10$
	PULL_L  D0/D1/A0/A3
	RET

	ENDC	;!wantMOBILES

*------------------------------------------
    IFD	wantMOBILES		;we have a different routine

	XDEF	CHKPAUSE

CHKPAUSE

* npc in d0
* Exit;
*	NE <=> paused.

	MOVE.L	A3,-(A7)
	DO	GETNPC			;->a3
	BTST	#0,3(A3)		;PAUSED?
	MOVE.L	(A7)+,A3
	RET
	

    ENDC	;wantMOBILES
*------------------------------------------

	
	IFNE	THE_PAWN
	IFND	wantMOBILES		;mobiles are handled differently
	
*--------------------------------
*
* MOVENPC	LETS NPC'S WALK
*			ABOUT
*
* ON ENTRY:
*
*   D0 - NOUN NO.
*   A0 - POINTER TO NOUN DATA
*   A6 - POINTER TO TIME MAP
*		(ACTUALLY TO CUR. LOC.)
*
*
* ON RETURN
*
*   D1/D2 STEPPED ON
*   8(A0), (A6), 2(A6) UPDATED
*   LOC	CLOC  OFST
*
*
*--------------------------------

	XDEF	MOVENPC
 
MOVENPC
 
	CALL	CHKPAUSE		;NOT DOING ANYTHING ELSE
	BNE.S   90$	
	TEST_W  (A6)			;IF CUR  LOC=0 THEN STAY STILL
	BEQ.S   90$
	CLR.L   D2
	MOVE.W  2(A6),D2		;OFSET INTO MAP
	CLR.L   D1
	MOVE.B  4(A6,D2),D1		;NEW ROOM NO.
	BEQ.S   10$			;ROOM #0 MEANS STOP OR LOOP
	MOVE.W  D1,(A6)
	MOVE.W  D1,D2			;FOR R.NOUN2
	MOVE.L  D0,-(A7)		;SAVE D0
	DO	R.NOUN2
	MOVE.W  D0,8(A0)		;NEW ROOM NOUN#
	MOVE.W  #$0100,6(A0)		;RELATED
	MOVE.L  (A7)+,D0		;RECOVER D0
	ADD.W   #1,2(A6)		;NEXT LOC
	RET
 
10$
	TEST_B  5(A6,D2)		;00 00 00 MEANS STOP
	BNE.S   20$
	TEST_B  6(A6,D2)
	BNE.S   20$
	CLR.W   (A6)			;SET CUR LOC TO ZERO = STOP
	RET
 
20$
	MOVE.B  5(A6,D2),2(A6)		;HIGH BYTE NEW OFFSET
	MOVE.B  6(A6,D2),3(A6)		;LOW BYTE
90$
	RET



	ENDC	;!wantMOBILES
	ENDC


*--------------------------------

	XDEF	UNPAUSE
	XREF	NPC.DATA
   
UNPAUSE

	MOVE.L  A0,-(A7)
	LEA	NPC.DATA(A4),A0
10$
	BCLR	#0,3(A0)
	TEST_L  (A0)+
	BNE.S   10$
	MOVE.L  (A7)+,A0
	RET

*------------------------------------------

    ENDC	;!wantNONPCPAUSE

 

	END
