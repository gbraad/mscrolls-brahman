******************************************************
*
*	@(#)read.elt	1.3
*	10/27/87
*
******************************************************

		SECTION		"ADV",CODE

*--------------------------------
*
*      SRC.ADV.READ
*
*--------------------------------

	include	"equates.i"
	include	"nounequ1.i"
	include	"nounequ2.i"
	include	"macros.i"
	include	"verbequ.i"

	include	"scenequ.i"

      XREF     W.IDFY,YCANT,W.BECAUS,P.PRN,ISARE,W.CLOSED,SP.READ,BSFULLSTOP
      XREF     CONJ,W.MINDRD,W.NTREAD,P.SCN,P.DESC,DO.SCN,MessageFromTable

	IfVerb	READ

	XDEF     V.READ

V.READ

      IFND	wantREADCONJ
 
	CMP.W   #-1,CONJ(A4)
	BEQ.S	0101$
	MSGGO	IDFY 
0101$
	
      ENDC

      IFND	wantREADifCLOSED

	AND.B   #$60,D1            ;CANNOT READ IF LOCKED OR CLOSED
	BEQ.S   10$
	DO	YCANT
	MSG	BECAUS
	DO	P.PRN
	DO	ISARE
	MSG	CLOSED
	DOGO	BSFULLSTOP
    
      ENDC

10$
	CALL    SP.READ
	TST_NPC				;NPC?
	BEQ.S	0102$
	MSGGO	MINDRD
0102$
 
*--------------------------------

      IFEQ	THE_PAWN

	XREF	TBL.READ

	LEA	TBL.READ(A4),A1
	DO	MessageFromTable	;Do any message
	BNE.S	0103$
	MSGGO	NTREAD			;if none, then can't read that
0103$
	RET  

      ENDC

      IFNE	THE_PAWN

	LEA     TBL.READ(A4),A1
30$
	CMP.W   (A1)+,D0
	BEQ.S   40$
	TEST_W  (A1)+
	BNE.S   30$
 
  
	CMP.W   #NTOMES,D0
	BEQ     READ.TOMES

*--------------------------------
 
	MSGGO	NTREAD
40$
	MOVE.W  (A1),D1
	BPL     P.SCN
	AND.W   #$7FFF,D1          ;IF HI BIT SET THEN USE A NOUN DESC
	MOVE.W  D1,D0
	BRA     P.DESC
 
TBL.READ 
 
	DC.W     NPAPER,NPAPER+$8000
	DC.W     NPOSTER,NPOSTER+$8000
	DC.W     NGRAFFITTI,NGRAFFITTI+$8000
	DC.W     NWRITING,NWRITING+$8000
	DC.W     NBOARD,NWRITING+$8000
	DC.W     NPWALL,NGRAFFITTI+$8000
	DC.W     NWRITING2,NWRITING2+$8000
	DC.W     NNOTICE,NNOTICE+$8000
	DC.W     NMESSAGE,NMESSAGE+$8000
	DC.W     NPLAQUE,NPLAQUE+$8000
	DC.W     NREM,NREM+$8000
	DC.W     NCHIT,NCHIT+$8000
	DC.W     NLISTING,NLISTING+$8000
	DC.W     0 
 
 
*--------------------------------
 
 
   
*--------------------------------
 
READ.TOMES
 
	BSET    #7,6(A0)
	SCN     87
	RET

      ENDC

	ENDC

      END

