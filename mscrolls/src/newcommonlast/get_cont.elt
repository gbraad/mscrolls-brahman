**************************************************************
*
*	2/20/89
*	@(#)get_cont.elt	2.2
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include "scenequ.i"

*--------------------------------
*
* GET_CONT -	GETS ALL THE NOUNS
*		WHICH ARE CONTAINED
*		BY D0
*
* LEAVES THE LIST POINTED TO BY A1
* ZERO (WORD) TERMINATED
*
* Warning!!!! - list contains hi-bit set noun#'s of non-contained objs
*
*--------------------------------

	XDEF	GET_CONT

      IFD	wantGET_CONT2

	XDEF	GET_CONT2
* return non local contents
GET_CONT2
	PUSH_L	D0-D6/A0/A2-A6
	ST	D6			;include non-local nouns
	BRA.S	Common_GET_CONT
 
      ENDC	;wantGET_CONT2

GET_CONT
  
* OF D0
	PUSH_L  D0-D6/A0/A2-A6

      IFD	wantGET_CONT2
	SF	D6			;local nouns only
Common_GET_CONT
      ENDC	;wantGET_CONT2

	GETINFO
	MOVE.B  10(A0),D1
	AND.B   #7,D1			;VOLUME
	OR	#1,CCR
	BEQ.S   99$
	AND	#$FE,CCR   
 
	MOVE.W  D0,D2			;TARGET

      IFD	wantGET_CONT2
	TEST_B	D6			;local or all?
	BEQ.S	08$
	DO	GETRELN2		;include nonlocal
	BRA.S	09$
08$
      ENDC	;wantGET_CONT2

	DO	GETRELN			;TO A1
09$
	MOVE.L  A1,A3
10$
	MOVE.W  (A1)+,D0
	BEQ.S   90$			;DONE
	GETINFO
	TST_OUT				;out?
	BNE.S	20$			;not a chance
	TST_CONTAINED				;CONTAINED?
	BNE.S   10$			;YES, OK
20$
	BSET	#7,-2(A1)
	BRA.S   10$
90$
	MOVE.L  A3,A1
99$
	PULL_L  D0-D6/A0/A2-A6
	RET
 

	END
