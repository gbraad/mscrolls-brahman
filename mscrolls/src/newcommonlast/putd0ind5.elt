**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"scenequ.i"

*
*
* PUTD0IND5 - move D0 into D5, fixing all cont. vols etc
*
* this does no checking!
*

	XDEF	PUTD0IND5,PUTD0IND5_2

	XREF	ISATTRIBUTE,P.SEPA

PUTD0IND5

	MOVE.W	D4,-(A7)
	ST	D4			;insist
	CALL_S	PUTD0IND5.SUB
	MOVE.W	(A7)+,D4
	RET

PUTD0IND5_2

	MOVE.W	D4,-(A7)
	CLR.B	D4			;put in room if necessary
	CALL_S	PUTD0IND5.SUB
	MOVE.W	(A7)+,D4
	RET

PUTD0IND5.SUB

	PUSH_L	D0-D3/D5/A0-A1/A5
	
    IFD		wantPUTD0IND5toSETIN
	GETINFO
	TST_OUT				;if no is out, no need to adjust vol.
	BNE.S	01$			;fine.
	DO	FIXCONT			;fix volume of container removed from
    ENDC	;wantPUTD0IND5toSETIN
	
01$
	GETINFO
	MOVE.B	2(A0),D2
	LSR.B	#4,D2			;get size
	EXG     D0,D5
	EXG	A0,A5			;d0 = container, d5 = object
	GETINFO

    IFD	attribute_INFINITE_CONTAINER
	XREF	Denotation_INFINITE_CONTAINER
	LEA	Denotation_INFINITE_CONTAINER(A4),A1
	DO	ISATTRIBUTE		;but do we modify the volume left?
	BEQ.S	80$			;if infinite our work is done.
    ENDC	;attribute_INFINITE_CONTAINER

	MOVE.B  10(A0),D3		;cap
	AND.B	#7,D3			;just capacity
	SUB.B   D2,D3			;with new cap
	CMP.B	#1,D3			;must still be >=1
	BGE.S	10$			;yes, ok
	TEST_B	D4			;do we insist it goes in?
	BNE.S	09$			;yes
	BTST	#5,6(A0)		;container contained?
	BEQ.S	08$			;nom then goes in room
	MOVE.W	8(A0),D0		;get next container
	EXG	D0,D5			;put d0 in d5 instead
	BRA.S	01$			;and keep going
08$
	MOVE.B	1(A5),D1
	LSR.B	#4,D1			;get hardness of object
	CMP.B	#1,D1			;liquids dont go in rooms!
	BNE.S	07$			;not, then ok
	MOVE.W	D5,D0			;back to d0
	DO	VALID.NP		;is it around then?
	BEQ.S	06$			;yes, then u see it seep away
	CLR.L	6(A5)			;has no location (fixcont is ok then)
	DO	SETOUT			;else fod it
	BRA.S	90$			;exit
06$
	CLR.L	6(A5)			;has no location (fixcont is ok then)
	CALL	P.SEPA			;it seeps away
	BRA.S	90$			;hoho
07$
	DO	TOPLOC			;eventual room ->d0
	EXT.L	D0			;incase, make full location
	MOVE.L	D0,6(A5)		;object is in that room now
	BRA.S	90$
09$
	MOVEQ	#1,D3			;otherwise fake it
10$
	MOVE.B  D3,10(A0)		;update	 container vol
80$
	MOVE.W	#$2000,6(A5)		;object contained.
	MOVE.W	D0,8(A5)		;in container.
90$
	PULL_L	D0-D3/D5/A0-A1/A5
	RET

*------------------------------------------
*
* DropHere - drops the noun (in D0) in the current room
*            it also fixes container volumes
*

	XDEF	DropHere
	XREF	ROOM

DropHere

	MOVE.L	A0,-(A7)
	GETINFO
	DO	FIXCONT
	CLR.W	6(A0)
	MOVE.W	ROOM(A4),8(A0)
	MOVE.L	(A7)+,A0
	RET


	END
