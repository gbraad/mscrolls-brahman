**************************************************************
*
*	4/5/89
*	@(#)empty.elt	2.6
*
**************************************************************


	SECTION	"ADV",CODE


	include	"equates.i"
	include	"macros.i"
	include	"verbequ.i"
	include	"scenequ.i"


	IfVerb	EMPTY

	XDEF	EMPTY
	XREF	SP.EMPTY,V.PUT
	XREF	STEADFAST,WHOOPS,ROOM,CONJ,VERB

 
EMPTY
 
	CALL	SP.EMPTY		;STOP EMPTY BALLOT BOX THINGS
 
	MOVE.W  D0,D6			;SAVE 1ST CONAINER #
	TEST_W  D5			;ADDNOUN?
	BEQ.S   30$			;NONE

    IFD	wantEmptyOnto

	MOVE.W	CONJ(A4),D1		;get prep (if exists)
	CMP.W	#OVER,D1
	BEQ.S	05$
	CMP.W	#ON,D1
	BEQ.S	05$
	CMP.W	#ONTO,D1
	BNE.S	10$
05$
	DO	CHK4PT			;OK?
	MOVE.W	#ONTO,CONJ(A4)		;keep v.put happy
	ST	D7			;so uses put
	BRA.S	41$
    ENDC	;wantEmptyOnto

10$
	MOVEQ   #INTO,D1		;INTO?
	DO	CHK4PT			;OK?

	ifne	0
	MOVE.B  10(A5),D1		;VOL
	AND.B   #7,D1
	BNE.S   20$
	MOVE.W  D5,D0
	BRA.S	10$
	endc

	ST	D7			;we wil use put
	BTST	#5,(A5)			;is the target open?
	BEQ.S	41$			;NE => nope - complain
	MOVE.W	D5,D0			;complain about I.O.
	BRA.S	42$
30$
	MOVEQ	#0,D7			;flag, use internal EMP50
	TST_PSEUDO			;is container a pseudo?
	BEQ.S	31$			;no, things are simple today.
	MOVEQ	#0,D3			;clear 1st, will be a location.
	MOVE.W	ROOM(A4),D3		;into THIS room
					;NB: this will be wrong if
					;SUBJECT != 0.
	BRA.S	41$			;use our EMP50
31$
	MOVE.L	6(A0),D3		;target location is that of container.
	TEST_W	6(A0)			;is container location simple?
	BEQ.S	41$			;flags set-up for move to room.
	MOVE.W	D3,D5			;will be target if use PUT.
	TST_CONTAINED			;is container conytained?
	BEQ.S	3101$			;no.
	MOVE.W	#IN,CONJ(A4)		;put x IN y.
	ST	D7			;flag to use put.
	BRA.S	41$			;and continue.
3101$
	PUSH_L	D0/A0
	MOVE.W	D3,D0			;the located object.
	GETINFO				;make sure it is not a room.
	TST_SITTABLE			;is location of container a surface?
	BEQ.S	3102$			;no.
	AND.L	#$0100FFFF,D3		;simply related to surface.
	PULL_L	D0/A0			;restore.
	BRA.S	41$			;and do it.
3102$
	DO	TOPLOC			;ultimate room of container.->d0
	MOVE.W	D0,D3			;will be a location
	EXT.L	D3			;assume d3 is positive.->d3=loc of room
	PULL_L	D0/A0			;restore original container.
41$
	TST_CLOSED			;CLOSED?
	BEQ.S   45$			;EQ= NO
42$
	DO	P.BTN			;BUT THE XYZZY,.
	GETINFO
	DO	ISARE			;'IS
	MSGGO	CLSE			;'CLOSED.
45$
	DO	GET_CONT
	BCC.S   46$			;CS = NOT A CONTAINER
    IFD	wantPourAsEmpty
	MOVE.B	1(A0),D1
	LSR.B	#4,D1			;hardneds
	BEQ.S	47$			;void => nogo.
	CMP.B	#2,D1			;solid?
	BHI.S	47$			;yes, fail
	TST_CONTAINED			;in a container?
	BEQ.S	47$			;no, fail
	MOVE.W	8(A0),D0		;get container
	GETINFO				;and it's data.
	CMP.W	D0,D5			;empty liquid to its container?
	BEQ	30$			;yes, figure out.
	BRA.S	45$			;andf consider that instead.
    ENDC	;wantPourAsEmpty

47$
	DO	P.BTN
	DO	ISAREN
	MSGGO	NCONT
46$
	MOVEQ   #0,D4			;COUNT NO. OF OBJS
	TEST_B	D7				;are we using PUT?
	BEQ.S	EMP50				;no.
	EXG	A0,A5
	EXG	D0,D5				;save our dobj.
	GETINFO
	EXG	D0,D5
	EXG	A0,A5
EMP50
	MSG	LINE			;'cos lots of info produced
	MOVE.W  (A1)+,D0		;GET OBJ
	BMI.S   EMP50			;FLAGGED OUT
	BEQ.S   70$			;FINITO
	GETINFO		
 
	TEST_W	D7			;is there a target?
	BEQ.S	10$			;EQ => no, put in room/on inv

	PUSH_L	D0-D7/A0-A6		;I don't trust V.PUT!!
	CALL	V.PUT			;so SP.PUT etc get called
	PULL_L	D0-D7/A0-A6
	TEST_B	WHOOPS(A4)		;do we wish to quit, here
	BNE	90$			;yes, ret
	BRA.S	60$

10$

      IFD	wantSP.EMPTY2
	XREF	SP.EMPTY2

	CALL	SP.EMPTY2
	BEQ.S	EMP50
      ENDC	;wantSP.EMPTY2

	LIQUID	D1			;?
	BEQ.S	57$
	TST_MOVEABLE			;moveable?
	BEQ.S	20$
	TST_INSEPARABLE			;inseperable?
	BEQ.S	30$
20$
	CALL	STEADFAST
	MOVEQ	#-$7F,D4		;set neg. count
	BRA.S	EMP50
30$
	DO	FIXCONT
	MOVE.L	D3,6(A0)		;use original location of container
	BRA.S	60$
57$
	DO	P.TN			;YES - THE XYZ SEEPS AWAY..
	MSG	SEPAWY		
	DO	SETOUT
60$
	ADDQ.W  #1,D4			;COUNT NOUNS REMOVED
	BRA.S   EMP50			;NEXT
70$
	MOVE.W  D6,D0			;RECOVER NOUN # OF 1ST CONTAINER
	GETINFO		
	TEST_W  D4			;WAS IT EMPTY?
	BNE.S	80$
	DO	P.BTN			;BUT THE XYZ DIDNT CONTAIN ANYTHING
	DO	ISARE
	MSGGO	DCONTA			;'EMPTY'
80$
	BMI.S	90$			;cos we had a steadfast..
	TEST_W	D7			;did we put it into something?
	BNE.S	90$
    IFD		wantPourAsEmpty
	CMP.W	#VNPOUR,VERB(A4)        ;if pour dont mention it.
	BEQ.S	90$
    ENDC	;wantPourAsEmpty

	MOVE.W	D6,D0
	DO	P.TN			;the xyzzy is now empty
	DO	ISARE
	MSG	NOW
	MSG	DCONTA
90$
	RET

 
	ENDC


	END
