**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"

	XDEF	PRTTXT
	XREF	STACK

PRTTXT
 
	MOVE.L  (A7),STACK(A4)    ;PULL DATA ADDRESS
	PUSH_L  D0-D7/A0-A6
	MOVE.L  STACK(A4),A3       ;REPLACE
10$
	MOVE.B  (A3)+,D4           ;GET 1 CHARS.
	BEQ.S   90$                ;FINISH
	CALL_S  PRTBYTE
	BRA.S   10$
90$
	ADDQ.L  #1,A3
	MOVE.L  A3,STACK(A4)
	BCLR    #0,STACK+3(A4)
	PULL_L  D0-D7/A0-A6
	MOVE.L  STACK(A4),(A7)    ;PUSH RETURN ADDRESS
	RET

	XDEF	PRTBYTE
	XREF	CHAROUTL
	
PRTBYTE
 
	TEST_B  D4                 ;CHAR OR WORD ?
	BMI.S   PRINTWRD
	MOVE.B  D4,D1              ;BYTE TO SEND
	BRA     CHAROUTL           ;FUDGE TO LOWER CASE
	
	XDEF	PRINTWRD
	XREF	WRDTABLE,SUBJECT,ST_

PRINTWRD
  
	MOVE.L	D0,-(A7)
	DO	P.SUB
	AND.W   #$7F,D4            ;GEN OFFSET INTO WORD ADDRESS TABLE
	LSL.W   #$01,D4            ;*2
	LEA     WRDTABLE(A4),A2    ;START OF TABLE
	ADDA.W  D4,A2              ; ADD OFFSET TO START
	LEA     ST_(A4),A1         ;OF TABLE
	ADD.W   (A2)+,A1           ; GET ADDRESS OF STRING
	MOVE.W	SUBJECT(A4),D0
	BEQ.S   10$                ;OK - ITS NORMAL
	GETINFO
	TST_PLURAL		;plural is like 3rd person
	BNE.S	10$
30$
	TEST_B  (A1)+              
	BNE.S   30$                ;SKIP FIRST MSG
	
10$
	MOVE.B  (A1)+,D1
	BEQ.S   20$                ;DONE
	DO	CHAROUT
	BRA.S   10$                ;NEXT
20$
	MOVE.L	(A7)+,D0
	RET
 
*--------------------------------



	END
