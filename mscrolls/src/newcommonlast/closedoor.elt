**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"


*
* CloseDoor - closes the door given in D0
*
	XDEF	CloseDoor,CloseDirection
	XREF	DOORS,PSUDAT,CONNECT2

CloseDoor

	PUSH_L	D1-D2/A0/A2
	MOVEQ	#-1,D2		;normal msg - the door is closed
	CALL_S	CloseDirection
	PULL_L	D1-D2/A0/A2
	RET


*
* CloseDirection takes params D0 = noun# of door, D2 = nway_xyzzy: the
*                message to be given when u try to pass it (closed)

CloseDirection

	PUSH_L	D2/A0/A2
	GETINFO

	BCLR    #4,(A0)
	BSET    #5,(A0)
 
	LEA     DOORS(A4),A1
10$
	CMP.W   (A1),D0           	;FIND DOOR
	BNE.S   20$

	PUSH_L	D0/A0/A1

	CLR.L   D0
	LEA     PSUDAT(A4),A2
	ADD.W   8(A0),A2          	;POINTER TO PSEUDO DATA
	 
	MOVE.B  (A2)+,D0          	;ROOM NO. OF ONE SIDE OF DOOR
	MOVE.B  2(A1),D1          	;DIRECTION 1-2
	DO	CONNECT2
	CLR.L   D0                	;GOING TO BE MULT BY 10
	MOVE.B  (A2),D0           	;ROOM ON OTHER SIDE OF DOOR
	MOVE.B  3(A1),D1          	;DIRECTION 2-1
	DO	CONNECT2	    

	PULL_L	D0/A0/A1

20$
	TEST_L  (A1)+
	BNE.S   10$
	PULL_L	D2/A0/A2
	RET
 


	END
