**************************************************************
*
*	11/4/87
*	@(#)p_exits.elt	1.7
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"scenequ.i"
	include	"linemacros.i"
	include "nway.i"

	XDEF	P.EXITS
	XREF	L.DIREC

 
P.EXITS
 
	CLR.W   D4
	DO	CALCNET			;A0 POINTS TO CUR ROOM IN NETWORK

      IFND	wantBIGPROG
	LEA     L.DIREC(A4),A1		;A1 POINTS TO TEXT IN WORDTABLE
      ENDC
      
      IFD	wantBIGPROG
	LEA     L.DIREC,A1		;A1 POINTS TO TEXT IN WORDTABLE
      ENDC
      
	MOVEQ   #DIR_D+1,D0		;Literal directions not in/out

20$
	MOVE.B  (A0)+,D1		;GET A DIREC
	BEQ.S   50$			;ZERO = CAN'T GO THAT WAY


      IFEQ     THE_PAWN

       IFND	wantPWAYS	
	CMP.B   #MAXROOM,D1
       ENDC

       IFD	wantPWAYS
	CMP.B	#MAXPWAY,D1
       ENDC
	BCC.S   50$

      ENDC


      IFNE     THE_PAWN

	BMI.S   50$                ;NO WAY - BUT SPECIAL MSG

      ENDC

	ADDQ.B  #1,D4               ;FLAG FOR NO EXITS

10$
	CMP.B   #1,D4		;first dir?
	BEQ.S   14$		;yes, don't print a comma,
	MSG	COMMA		;print a comma

14$
      IFNE	YCHEAT
	
	XREF	CHEAT,PRTHEX

	TEST_B	CHEAT(A4)
	BEQ.S	15$
	PUSH_L	D0-D7/A0-A6
	PRINT	<'^('>
	MOVEQ	#0,D2
	MOVE.B	D1,D2
	MOVE.W	D2,-(A7)		;save room# to be printed
	DO	R.NOUN2
	DO	P.NOUN
	MOVE.W	(A7)+,D6		;get room#
	CALL	PRTHEX
	PRINT	<') '>
	PULL_L	D0-D7/A0-A6

      ENDC

15$
      IFND	wantBIGPROG
	MOVE.B  (A1)+,D1            ;PRINT DIR TEXT
      ENDC

      IFD	wantBIGPROG
	GetWTabByte			;like MOVE.B (A1),D1
	ADDQ.L	#1,A1
	TEST_B	D1
      ENDC

	BMI.S   16$                 ;LAST CHAR
	DO	CHAROUT
	BRA.S   15$
16$
	DO	CHAROUT
	BRA.S   30$
50$
      IFND	wantBIGPROG
	MOVE.B  (A1)+,D1            ;PRINT DIR TEXT
      ENDC

      IFD	wantBIGPROG
	GetWTabByte			;like MOVE.B (A1),D1
	ADDQ.L	#1,A1
	TEST_B	D1
      ENDC

	BPL.S   50$ 
	ADDQ.L  #1,D1
30$
	SUBQ.B  #1,D0
	BNE.S   20$
	TEST_B  D4
	BNE.S   40$
	MSG     NWHERE          ;D4=0 = NO EXITS
	BRA.S   55$
40$
	DOGO	FULLSTOP
55$
      IFNE	YCHEAT
       
       IFEQ	THE_PAWN
	
	TEST_B	CHEAT(A4)
	BEQ.S	99$
	PUSH_L	D0-D7/A0-A6
	PRINT	<'^in - '>
	MOVEQ	#0,D2
	MOVE.B	(A0)+,D2
	BEQ.S	101$
	CMP.B	#MAXROOM,D2
	BCC.S	101$
	MOVE.L	D2,-(A7)
	DO	R.NOUN2
	MOVE.L	(A7)+,D6
	CALL	PRTHEX
101$
	PRINT	<'^out - '>
	MOVEQ	#0,D2
	MOVE.B	(A0)+,D2
	BEQ.S	102$
	CMP.B	#MAXROOM,D2
	BCC.S	102$
	MOVE.L	D2,-(A7)
	DO	R.NOUN2
	MOVE.L	(A7)+,D6
	CALL	PRTHEX
102$
	PULL_L	D0-D7/A0-A6

       ENDC
      ENDC

99$
	MSGGO     LINE


	END
