**************************************************************
*
*	10/27/87
*	@(#)scan.elt	1.2
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"

*--------------------------------
*
* SCAN - FINDS NOUNS WHICH ARE
*        HIDDEN BY NOUN IN D0
*
* EG, PLANT POT ON BENCH
*     COIN ON CUSHION
*
* WANTS D0=NOUN#
* IT PRINTS OUT IF NESS. & SETS
* PRONOUNS AS PER DISCOVERED ITEM
*
*--------------------------------

	XDEF	SCAN
	XREF	ROOM,SP3.FLAG,SETPRON,SCANFLAG
 
SCAN
	MOVEQ   #0,D7              ;ERR FLG
	MOVE.W  D0,D2              ;TARGET
	ST      SCANFLAG(A4)
	DO	GETRELN	           ;GETS RELATED NOUNS (D5=1,D1=8,GETNOUNS)
	CLR.W   SCANFLAG(A4)
10$
	MOVE.W  (A1)+,D0           ;GET NOUN
	BEQ.S   90$
	GETINFO                    ;ON D0
	TST_HIDDEN				;HIDDEN
	BEQ.S   10$
	TEST_W  SP3.FLAG(A4)       ;SUPRESS OUTPUT?
	BNE.S   76$
	CALL    SETPRON            ;SET HE,SHE,IT ETC AS REQUIRED

      IFEQ	THE_PAWN

	XREF	ScanSpecials

	CALL	ScanSpecials		;so it can say, u grovel around..
	BEQ.S	76$			;EQ => we said something, so shutup
     
      ENDC

	DO	P.SUB	             ;TELL THE LUCKY PLAYER
	MSG     FIND
	DO	P.QN

      IFEQ	THE_PAWN

	DO	BSFULLSTOP

      ENDC
76$
	BCLR    #1,6(A0)           ;TO ITEM
	MOVE.B  6(A0),D7
	BNE.S   10$                ;EXIT
	MOVE.W  ROOM(A4),8(A0)     ;THEN IS IN ROOM
	MOVEQ   #1,D7              ;DONE
	BRA.S   10$                ;NEXT
90$
	TEST_W  SP3.FLAG(A4)       ;STOP NHAP MES?
	BEQ.S   99$
	MOVEQ   #1,D7              ;FAKE ITEM FOUND
99$
	RET
  
*--------------------------------
* 
* SCANSV   
*
*   SAVES REGS AND CALLS SCAN
*
*
*--------------------------------

	XDEF	SCANSV
 
SCANSV
 
	PUSH_L  D0-D7/A0-A6
	CALL_S  SCAN
	PULL_L  D0-D7/A0-A6
	RET
  
  



	END
