**************************************************************
*
*	%G%
*	%W%
*
**************************************************************


	SECTION	"ADV",CODE

	include	"equates.i"
	include	"macros.i"
	include	"verbequ.i"
	include	"scenequ.i"

*
* V.SWIM
*
	IfVerb	SWIM

	XDEF	V.SWIM
	XREF	SP.SWIM,R.NOUN,P.SUBV,DBS,ADVERB
	XREF	GO.N,GO.OK

V.SWIM


	CALL	SP.SWIM
	CALL_S	IsRoomWet
	BEQ.S	10$			;cant swim here
	TEST_W	D0			;dobj specified?
	BPL	GO.N			;yes, try going there
	MOVE.W	ADVERB(A4),D1		;fetch modifier
	BPL.S	20$
	CMP.W	#-1,D1			;null?
	BEQ.S	20$
	BCLR	#15,D1			;fetch direction
	MOVE.W	D1,D5
	SUBQ.L	#4,A7			;go will pop this
	BRA	GO.OK			;try to go there
10$
	DO	P.SUBV
	MSG	UNABLE
	MSGGO	HERE			;fullstop
20$
	DO	P.SUB
	PRINT	<'swim around a bit.'>
90$
	RET

*------------------------------------------

	XDEF	IsRoomWet
	XREF	R.NOUN,SEATED,SEATED_RELATOR

IsRoomWet
	
* Exit	NE<=>wet

	TEST_W	SEATED(A4)
	BEQ.S	10$		;not so
	CMP.W	#IN,SEATED_RELATOR(A4)	;sitting in something?
	BEQ.S	90$			;yes, exit as not wet
10$
	PUSH_L	D0/D2/A0
	DO	R.NOUN		; noun# of room ->d0
	GETINFO
	TST_WET				;
	PULL_L	D0/D2/A0
90$
	RET

*------------------------------------------

	ENDC



	END
