10 POKE51,0:POKE52,16:POKE55,0:POKE56,16:IFF%=1THEN30
20 CLR:F%=1:LOAD"GMC",8,1
30 I$="AG":PRINT"GAME COPY PROGRAM"
40 FORD=1TO2:D%=D:OPEN1,8,15:GOSUB180:GOSUB220
50 SR%=682:SW%=SR%
60 BM%=234
70 B%=0
80 GOSUB210:OPEN2,8,2,"#"
90 GOSUB250:SR%=SR%-1:B%=B%+1
100 IF(B%<>BM%)AND(SR%>=0)THEN90
110 B%=0:CLOSE2
120 GOSUB180:OPEN2,8,2,"#"
130 GOSUB270:SW%=SW%-1:B%=B%+1
140 IF(B%<>BM%)AND(SW%>=0)THEN130
150 CLOSE2
160 IFSW%>=0THEN70
170 I$="SW":CLOSE1:NEXTD:END
180 PRINT:PRINT"INSERT DESTINATION DISK ";D%
190 INPUTA$
200 RETURN
210 PRINT:PRINT"INSERT GAME MASTER DISK ";D%:GOTO190
220 PRINT"FORMATTING.."
230 PRINT#1,"N:COPY,"+I$:GOSUB300:IFE%<>0THEN230
240 RETURN
250 S%=SR%:GOSUB330:PRINT#1,"U1 2 0";T%;S%:GOSUB300:IFE%<>0THEN250
260 POKE4096,B%:SYS4097:PRINT"R";:RETURN
270 PRINT#1,"B-P 2 0":POKE4096,B%:SYS4100:S%=SW%:GOSUB330:PRINT#1,"U2 2 0";T%;S%:PRINT"W";
280 GOSUB300:IFE%<>0THEN270
290 RETURN
300 INPUT#1,A,A$,B,C:E%=0:IFA=0THENRETURN
310 PRINTA;A$;B;C:PRINT"HIT A KEY TO RETRY":GOSUB190:E%=-1
320 CLOSE2:OPEN2,8,2,"#":RETURN
330 IFS%>=598THENT%=31+(S%-598)/17:S%=S%-((T%-31)*17+598):RETURN
340 IFS%>=490THENT%=25+(S%-490)/18:S%=S%-((T%-25)*18+490):RETURN
350 IFS%>=357THENT%=18+(S%-357)/19:S%=S%-((T%-18)*19+357):RETURN
360 T%=1+S%/21:S%=S%-((T%-1)*21):RETURN
