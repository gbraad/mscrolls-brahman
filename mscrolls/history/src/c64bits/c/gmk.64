10 TRAP 1490
20 MXF=64
30 I1(1)=ASC("S"):I2(1)=ASC("W")
40 I1(2)=ASC("A"):I2(2)=ASC("G")
50 FAST
60 PRINT CHR$(14)
70 INPUT "# OF ÄRIVES";ND
80 IF ND<>1 AND ND<>2 GOTO 70
90 DV=7+ND
100 BLOAD "WOBJ" ON U8, B0,P2816: REM AT B00
110 WRITESECT=2816
120 DIM STRACK%(MXF),SSECT%(MXF),NUMSEC%(MXF),WHERE%(MXF)
130 GOSUB 820: REM READ SECTOR PER TRACK DATA
140 FOR SD=1 TO 2
150 SD%=SD
160 PRINT"USE SIDE ",SD%," OF DISK"
170 IF ND=2 THEN PRINT"INSERT SOURCE DISK IN #8,DEST IN #9":GETKEY A$
180 RESTORE 990
190 CLOSE 15
200 GOSUB 1440: REM MAKE AN EMPTY DIRECTORY
210 GOSUB 1430: REM PROMPT FOR DISK TO WRITE
220 PRINT"ÆORMATTING DISK..."
230 OPEN 15,8,15,"N0:GUILD,"+CHR$(I1(SD%))+CHR$(I2(SD%))
240 IF DS<>0 THEN PRINT DS$:STOP
250 GOSUB 960: REM FOD THE BAM
260 PRINT
270 READ F$,FSIZE%,W%:REM GET A FILE NAME
280 IF F$="END" GOTO 1380
290 PRINTF$;"...."
300 GOSUB 710:REM  CALCULATE START OF FILE, FILE NUM.
310 I%=1:GOSUB 770:STRACK%(FILNUM%)=TRACK%:SSECT%(FILNUM%)=SECT%:NUMSEC%(FILNUM%)=FSIZE%:NUMSEC%(FILNUM%+1)=0:WHERE%(FILNUM%)=W%
320 IF (W%<>0) AND (W%<>SD%) GOTO 260
330 IF F$="DUMMY" THEN GOTO 370
340 GOSUB 1400: REM PROMPT FOR INPUT DISK
350 BLOAD (F$),ON U0+DV,B1,P2048+256:REM LOAD IT
360 IF DS<>0 THEN PRINT DS$;"-----";:GETKEY A$:GOTO 350
370 GOSUB 1430
380 OPEN 5,8,5,"#"
390 FOR I=1 TO FSIZE%
400 I%=I
410 GOSUB 770 : REM CONVERT I TO TRACK AND SECTOR
420 GOSUB 900: REM WRITE SECT%,TRACK% FROM FILE SECTOR I%
430 NEXT I
440 CLOSE 5
450 GOTO 260
460 CLOSE 15
470 END
480 PRINT#15,"B-P";5;0
490 PRINT#15,"U1";5;0;1;2
500 FOR I=1 TO 64
510 GET#5,A$:IFA$="" THEN A$=CHR$(0)
520 STRACK%(I)=ASC(A$)
530 GET#5,A$:IFA$="" THEN A$=CHR$(0)
540 SSECT%(I)=ASC(A$)
550 GET#5,A$:IFA$="" THEN A$=CHR$(0)
560 NUMSEC%(I)=ASC(A$)
570 GET#5,A$
580 NEXT
590 RETURN
600 OPEN 5,8,5,"#"
610 PRINT#15,"B-P";5;0
620 FOR I=1 TO MXF
630 PRINT#5,CHR$(STRACK%(I));
640 PRINT#5,CHR$(SSECT%(I));
650 PRINT#5,CHR$(NUMSEC%(I));
660 PRINT#5,CHR$(WHERE%(I));
670 NEXT
680 PRINT#15,"U2";5;0;1;2
690 CLOSE 5
700 RETURN
710 REM WORK OUT FILE NUMBER AND START SECTOR
720 SSEC%=0
730 FILNUM%=1
740 IF NUMSEC%(FILNUM%)=0 THEN RETURN
750 IF (WHERE%(FILNUM%)=0) OR (WHERE%(FILNUM%)=W%) THEN SSEC%=SSEC%+NUMSEC%(FILNUM%)
760 FILNUM%=FILNUM%+1:GOTO 740
770 REM CALC. SECT AND TRACK FROM START SEC, RELATIVE SEC
780 SECT%=I%+SSEC%-1
790 TRACK%=0
800 IF SECT%>=SPT%(TRACK%) THEN SECT%=SECT%-SPT%(TRACK%):TRACK%=TRACK%+1:GOTO 800
810 RETURN
820 RESTORE
830 DIM SPT%(35)
840 I%=1
850 READ COUNT,VL
860 IF COUNT=0 THEN RETURN
870 SPT%(I%)=VL:I%=I%+1: IF I%<=COUNT THEN GOTO 870
880 GOTO 850
890 DATA 17,21,18,0,24,19,30,18,35,17,0,0
900 REM WRITE A SECTOR
910 PRINT#15,"B-P";5;0
920 SYS WRITESECT,I%+8
930 PRINT#15,"U2";5;0;TRACK%;SECT%
940 PRINT I%,TRACK%,SECT%
950 RETURN
960 PRINT"ÆODDING THE ÂÁÍ"
970 PRINT"×E DON'T REALLY NEED TO DO THIS, SO IT AINT DONE YET"
980 RETURN
990 DATA BOOTLD,3,0
1000 DATA CODE REST,171,1
1010 DATA TEXT,241,1
1020 DATA INDEX,14,1
1025 DATA CAMEOS,58,1
1030 DATA RIVER,25,1
1040 DATA CASTLE,20,1
1050 DATA FIELD,21,1
1060 DATA HILL,19,1
1070 DATA INTEMPLE,16,1
1080 DATA ANTECHAMB,14,1
1090 DATA TEMPLE,19,1
1100 DATA TEMPCORR,17,1
1110 DATA STAIRWAY,19,1
1150 REM NOW PICTURES FOR SIDE 2
1155 DATA YARD,26,2
1160 DATA MILL,28,2
1170 DATA HOTHOUSE,20,2
1180 DATA SKELETON,20,2
1190 DATA BOATHOUSE,13,2
1200 DATA COALS,23,2
1220 DATA RAINBOW,8,2
1230 DATA CEMETERY,18,2
1240 DATA CORRIDOR,15,2
1250 DATA KITCHEN,21,2
1260 DATA BILLIARD,15,2
1270 DATA MUSIC,15,2
1280 DATA INBANK,23,2
1290 DATA BANKMAN,16,2
1300 DATA SUMP,27,2
1310 DATA SHOP,12,2
1320 DATA CRYPT,19,2
1330 DATA LOUNGE,18,2
1340 DATA ORGAN,22,2
1350 DATA OUTBANK,24,2
1355 DATA CODE PERM,72,2
1360 DATA END,0,0
1370 PRINTERR$(ER):GOTO 1380
1380 GOSUB 600: NEXT SD
1390 PRINT "ALL DONE":END
1400 PRINT"ÉNSERT DISK WITH FILES ON AND HIT <RETURN>";
1410 IF ND=2 THEN PRINT: RETURN
1420 GETKEY A$ :PRINT:RETURN
1430 PRINT"ÉNSERT BLOCK-FORMATTED DISK, AND HIT <RETURN>";:GOTO 1410
1440 FOR I=1 TO 64
1450 SSECT%(I)=0:STRACK%(I)=0:NUMSEC%(I)=0
1460 NEXT I
1470 SSECT%(1)=0:STRACK%(1)=1
1480 RETURN
1490 PRINTERR$(ER);EL:GETKEY A$:RESUME
