
$REDEFINE_VERB ask
 SCOPE all_locs

 PROLOGUE
   if not(testflag(o_actor.f_alive)) then
     printcr("Do you realize that you are talking to [a] [o_actor]?.")
     disagree()
   endif

   if equal(o_spec, o_nst) then
     printcr("I don't know what you're talking about.")
     disagree()
   endif

 "ask [o_subject] about [o_spec]"
   DISAMBIGUATION_RULES
    if equal(o_subject, o_nst) then score(-15) endif  # make nst always lose
    if testflag(o_spec.f_seenbefore) then score(5) endif
    if isobject(o_subject) then
      if owns(l_exit, o_subject) then
        if owns(l_exit, o_player) then 
          score(5)
        else
          score(-5)          
   END_RULES

   if equal(o_subject, o_actor) then
     printcr("Talking to yourself is a sign of pending mental collapse.")
   else
     if not(testflag(o_spec.f_seenbefore)) then
       printcr("We haven't found that yet.")
     else
       printcr("Just [a] [o_spec].")

  DEFAULT
    printcr("I only understood you as far as wanting to ask something.")
    if testflag(o_player.f_verb_help) then
      printcr("")
      Printcr("An example of the syntax I understand is:")
      printcr("  'ask Fred about the old wooden chest'")
      printcr("(to turn off these help messages, type 'help')")

ENDVERB


$REDEFINE_VERB is
 SCOPE all_locs

 "where is [o_subject]"
   DISAMBIGUATION_RULES
    if equal(o_subject, o_nst) then score(-10) endif  # make nst always lose
    if isobject(o_subject) then
      if owns(l_exit, o_subject) then
        if owns(l_exit, o_player) then 
          score(5)
        else
          score(-5)          
   END_RULES

   if cansee(o_actor, o_subject) then
     printcr("Right here!")
     disagree()
   endif

   if not(isobject(o_subject)) then
     printcr("You've been to [the] [o_subject], so it is somewhere on the map /
              that you must have drawn.")
     disagree()
   endif

   if testflag(o_subject.f_seenbefore) then
     if testflag(o_subject.f_alive) then
       printcr("[the] [o_subject] is a living creature. Could have gone /
                anywhere since your last encounter.")
     else
       print("[the] [o_subject] is [o_subject.r_preposition] [the]")
       print(owner(o_subject))
       printcr(".")
   else
     printcr("You haven't found that yet.")

 DEFAULT
   printcr("I didn't understand that sentence.")
   if testflag(o_player.f_verb_help) then
     printcr("")
     Printcr("An examples of the syntax I understand is:")
     printcr("  'where is the ball'")
     printcr("(to turn off these help messages, type 'help')")

ENDVERB

$REDEFINE_VERB goto
 SCOPE ALL_LOCS
 PROLOGUE
   if not(equal(o_actor, o_player)) then
     printcr("The 'goto' action only works for the player.")
     disagree()
   endif
   if equal(o_subject, %none) then
     agree()
   else
     if isobject(o_subject) then
       printcr("Goto can only be used with locations.")
       disagree()

 "goto [o_subject]"
   DISAMBIGUATION_RULES
    if equal(o_subject, o_nst) then score(-10) endif  # make nst always lose
    if not(isobject(o_subject)) then
      score(10)
    else
      if owns(l_exit, o_subject) then
        if owns(l_exit, o_player) then 
          score(5)
        else
          score(-5)          
   END_RULES

   # we should never get here, because the player object has
   # a trigger for "goto [o_subject]"
   printcr("You have to find the way to [the] [o_subject] by yourself.")

 DEFAULT
   printcr("I only understood you as far as willing to go somewehere.")

ENDVERB