
$VERB get SYNONYM take SYNONYM grab
 PROLOGUE
   if not(testflag(o_actor.f_alive)) then
     if trigger(o_actor.t_talk_to_dead) then
       disagree()
     endif
   endif

  if equal(o_subject, %none) then
    agree()
  endif

  if equal(o_actor, o_nst) then       # no such thing
    print("There is no one to take ")
    if equal(o_subject, o_nst) then
      print("[o_nst.d_any].")         # any such thing
    else
      print("[the] [o_subject].")
    endif
    disagree()
  else
    if equal(o_subject, o_nst) then
      # no such thing
      print("There is [o_nst.d_no] that [the] [o_actor] take.")
      disagree()
    endif
  endif
  agree()

 "get"
   if equal(o_actor, o_player) then
     printcr("What do you want to get?")
   else
     printcr("What do you want [the] [o_actor] to get?")
   endif
   getsubject()

 "get [o_subject]"
   DISAMBIGUATION_RULES
    if testflag(o_subject.f_takeable) then score(5) endif
    if not(owns(o_actor, o_subject)) then score(5) endif
   END_RULES

   if not(testflag(o_subject.f_takeable)) then
     printcr("[the] [o_subject] is not something that can be taken.")
   else
     if not(owns(o_actor, o_subject)) then
       setflag(o_subject.f_bypass)
       move(o_subject, o_actor)
       printcr("[o_subject]: taken.")
     else
       printcr("But [the] [o_actor] already has the [o_subject].")
     endif
   endif

 "get [o_subject] [prepos] [o_spec]"
   DISAMBIGUATION_RULES
    if testflag(o_subject.f_takeable) then
      if owns(o_spec, o_subject) then 
        score(10)
      else
        score(5)
   END_RULES

   if not(testflag(o_subject.f_takeable)) then
     printcr("[the] [o_subject] is not something that can be taken.")
   else
     if owns(o_spec, o_subject) then
       setflag(o_subject.f_bypass)
       move(o_subject, o_actor)
       printcr("[o_subject]: taken.")
     else
       printcr("[the] [o_actor] cannot get [the] [o_subject] [prepos] [the] [o_spec].")
     endif
   endif

 DEFAULT
   printcr("I only understood you as far as willing to get something.")
ENDVERB

