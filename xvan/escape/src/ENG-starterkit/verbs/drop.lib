
$VERB drop
 PROLOGUE
   if not(testflag(o_actor.f_alive)) then
     if trigger(o_actor.t_talk_to_dead) then
       disagree()
     endif
   endif

  if equal(o_subject, %none) then
    agree()
  endif

  if equal(o_actor, o_nst) then       # no such thing
    print("No one is holding ")
    if equal(o_subject, o_nst) then
      print("[o_nst.d_any].")         # any such thing
    else
      print("[the] [o_subject].")
    endif
    disagree()
  else
    if equal(o_subject, o_nst) then
      # no such thing
      print("[the] [o_actor] [o_actor.r_is] holding [o_nst.d_no] than can be dropped.")
      disagree()
    endif
  endif
  agree()

 EPILOGUE
   # check if they dropped the light source out of sight
   if not(islit(o_player)) then
     printcr("It is now pitch black.")

 "drop"
   if equal(o_actor, o_player) then
     printcr("What do you want to drop?")
   else
     printcr("What do you want [the] [o_actor] to drop?")
   endif
   getsubject()

 "drop [o_subject]"
   DISAMBIGUATION_RULES
    if owns(o_actor, o_subject) then score(10) endif
    if testflag(o_subject.f_takeable) then score(5) endif  # make o_nst loose
   END_RULES

   if owns(o_actor, o_subject) then
     clearflag(o_subject.f_bypass)
     move(o_subject, owner(owner(o_subject)))
     printcr("[o_subject]: dropped.")
   else
     if testflag(o_subject.f_takeable) then
       printcr("[the] [o_actor] [o_actor.r_is] not holding the [o_subject].")
     else
       printcr("[the] [o_subject] cannot be dropped.")

 "drop [o_subject] [prepos] [o_spec]"
   DISAMBIGUATION_RULES
    if owns(o_actor, o_subject) then score(5) endif
    if testflag(o_subject.f_takeable) then score(5) endif  # make o_nst loose
    if not(owns(o_actor, o_spec)) then score(5) endif
    if testflag(o_spec.f_container) and equal(%prepos, in) then score(5) endif
    if testflag(o_spec.f_supporter) and equal(%prepos, on) then score(5) endif
   END_RULES

   if try(l_location, 0, 1, "put [o_subject] [prepos] [o_spec]") then endif

 DEFAULT
   printcr("I only understood you as far as willing to drop something.")
ENDVERB

